! Meteo ruler

width  = 4.6
height  = 27
topMargin = 1
topTextPos = 0.55
bottomMargin = 0.5

maxh = 5600
fontdy = 0.1
vlinepos = width*0.59
majortic = 0.5
minortic= 0.3
textMargin = 0.1
boxdx = 0.01

hpax0 = -1.7
hpax1 = +1.7

rulerxspace = 0.2
rulern = 4

size (width * rulern + rulerxspace * (rulern - 1)) height

sub meterInFeet x
	return x * (1000/12/25.4)
end sub

sub FeetInMeter x
	return x / (1000/12/25.4)
end sub

sub hpaInMeter x
	return (1-(x/1013.25)^0.190284)*145366.45 / (1000/12/25.4)
end sub

sub meter
	begin origin
	set font ss
	local major, minor, h, i
	major = 100
	minor = 25
	h = height-topMargin-bottomMargin

	set just right
	for i = 0 to maxh step major
		amove -(majortic + textMargin) (i * h / maxh - fontdy)
		begin box fill white add boxdx nobox
		write i
		end box
	next i
	for i = 0 to maxh step minor
		amove 0 (i * h / maxh)
		if i % major = 0 then
			rline -majortic 0
		else
			rline -minortic 0
		end if
	next i
	end origin
end sub

sub feet
	begin origin
	set font ss
	local major, minor, h, i
	major = 500
	minor = 100
	h = height-topMargin-bottomMargin

	set just left
	for i = 0 to meterInFeet(maxh) step major
		amove (majortic + textMargin) (feetInMeter(i) * h / maxh - fontdy)
		begin box fill white add boxdx nobox
		write i
		end box
	next i
	for i = 0 to meterInFeet(maxh) step minor
		amove 0 (feetInMeter(i) * h / maxh)
		if i % major = 0 then
			rline majortic 0
		else
			rline minortic 0
		end if
	next i
	end origin
end sub

sub hpa x dx
	local h, hdx
	begin origin
	set font ssb
	set color rgb(100,0,0)
	set just right

	h = height-topMargin-bottomMargin
	amove (hpax0 - textMargin + dx) (hpaInMeter(x) * h / maxh - fontdy)
	write x
	amove (hpax1) (hpaInMeter(x) * h / maxh)
	rline (hpax0-hpax1+dx) 0

	end origin
end sub

sub punchCross
	begin origin
	amove -0.1 0
	rline 0.2 0
	amove 0 -0.1
	rline 0 0.2
	end origin
end sub

sub ruler
	gsave
	begin origin

	amove vlinepos bottomMargin

	hpa 1013.25 .5
	hpa 975 0
	hpa 950 0
	hpa 925 0
	hpa 900 0
	hpa 850 0
	hpa 800 0
	hpa 750 0
	hpa 700 0
	hpa 650 0
	hpa 600 0
	hpa 500 0
	hpa 550 0
	meter
	feet

	amove vlinepos bottomMargin
	rline 0 height-topMargin-bottomMargin

	set font ssb
	set just left
	amove (vlinepos + majortic + textMargin) (height - topTextPos)
	write "Feet"

	set just right
	amove (vlinepos - majortic - textMargin) (height - topTextPos)
	write "Meter"

	set just right
	set color rgb(1,0,0)
	amove (vlinepos + hpax0 - textMargin) (height - topTextPos)
	write "hPa"


	! gsave
	! amove .24 22.8
	! begin rotate 90
	! gsave
	! rmove 0 -.05
	! postscript gh.eps 0.2 0.2
	! grestore
	! set just left
	! set font ss
	! set color rgb(.5,.5,.5)
	! rmove 0.3 0
	! begin scale 0.5 0.5
	! text https://github.com/BlueMasters/meteoruler
	! end scale
	! end rotate
	! grestore

	set color rgb(.7,.7,.7)
	! last operation, cut the ruler
	amove 0 0
	box width height round 0.2

	end origin
	grestore
end sub

for i = 0 to 4
	ruler
	rmove width+rulerxspace 0
next i